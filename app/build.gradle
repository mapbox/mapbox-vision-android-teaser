apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: 'generate-token.gradle'

def localProperty(String name) {
    Properties properties = new Properties()
    def file = project.rootProject.file('local.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
        def localProperty = properties.getProperty(name)
        if (localProperty != null) {
            return localProperty
        }
    }
    return null
}

android {
    buildToolsVersion build_tools_version
    compileSdkVersion compile_sdk_version

    defaultConfig {
        applicationId "com.mapbox.vision.examples"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 8
        versionName "0.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a"
            universalApk false
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    signingConfigs {
        if (localProperty('SIGNING_CONFIG_RELEASE')) {
            release {
                storeFile file(localProperty('SIGNING_CONFIG_RELEASE'))
                storePassword localProperty('SIGNING_CONFIG_RELEASE_PASSWORD')
                keyAlias localProperty('SIGNING_CONFIG_RELEASE_ALIAS')
                keyPassword localProperty('SIGNING_CONFIG_RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "Debug Vision Teaser"]
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (localProperty('SIGNING_CONFIG_RELEASE')) {
                signingConfig signingConfigs.release
            }

            manifestPlaceholders = [appName: "Vision Teaser"]
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$android_x_appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$android_x_constraint_layout"

    implementation "androidx.multidex:multidex:$android_x_multidex"

    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:$mapbox_sdk"
    implementation "com.mapbox.mapboxsdk:mapbox-android-navigation:$mapbox_navigation"
    implementation "com.mapbox.mapboxsdk:mapbox-android-navigation-ui:$mapbox_navigation"
    implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-locationlayer:$mapbox_location_layer"
    implementation "com.mapbox.mapboxsdk:mapbox-android-core:$mapbox_core"

    if (localProperty('BUILD_CORE_FROM_SOURCE')?.toBoolean()) {
        implementation project(path: ':Vision')
        implementation project(path: ':VisionAr')
        implementation project(path: ':VisionSafety')
        implementation project(path: ':CoreAr')
        implementation project(path: ':CoreSdk')
        implementation project(path: ':CoreSafety')
    } else {
        implementation "com.mapbox.vision:mapbox-android-vision:$vision_sdk"
        implementation "com.mapbox.vision:mapbox-android-vision-ar:$vision_ar"
        implementation "com.mapbox.vision:mapbox-android-vision-safety:$vision_safety"
    }

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}
