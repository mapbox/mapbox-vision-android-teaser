apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: '../gradle/generate-token.gradle'
apply from: "../gradle/utils.gradle"

android {
    buildToolsVersion build_tools_version
    compileSdkVersion compile_sdk_version

    defaultConfig {
        applicationId "com.mapbox.vision.teaser"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 11
        versionName "0.4.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a"
            universalApk false
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    signingConfigs {
        if (projectOrLocalProperty('SIGNING_CONFIG_RELEASE')) {
            release {
                storeFile file(projectOrLocalProperty('SIGNING_CONFIG_RELEASE'))
                storePassword projectOrLocalProperty('SIGNING_CONFIG_RELEASE_PASSWORD')
                keyAlias projectOrLocalProperty('SIGNING_CONFIG_RELEASE_ALIAS')
                keyPassword projectOrLocalProperty('SIGNING_CONFIG_RELEASE_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "Debug Vision Teaser"]
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (projectOrLocalProperty('SIGNING_CONFIG_RELEASE')) {
                signingConfig signingConfigs.release
            }

            manifestPlaceholders = [appName: "Vision Teaser"]
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "androidx.appcompat:appcompat:$android_x_appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$android_x_constraint_layout"

    implementation "androidx.multidex:multidex:$android_x_multidex"

    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:$mapbox_sdk"
    implementation "com.mapbox.mapboxsdk:mapbox-android-navigation:$mapbox_navigation"
    implementation "com.mapbox.mapboxsdk:mapbox-android-navigation-ui:$mapbox_navigation"

    // don't use telemetry 4.5.1
    // https://github.com/mapbox/mapbox-navigation-android/issues/1984
    // TODO remove when fix is no more necessary
    implementation ("com.mapbox.mapboxsdk:mapbox-android-telemetry:$mapbox_telemetry") {
        force = true
    }
    // need 0.3.0 instead of 0.2.0 that maps 8.1.0 depends on to access `obtainVisionSkuUserToken` method
    // TODO remove when fix is no more necessary
    implementation ("com.mapbox.mapboxsdk:mapbox-android-accounts:0.3.0") {
        force = true
    }

    if (booleanProjectOrLocalProperty('BUILD_CORE_FROM_SOURCE')) {
        implementation project(path: ':Vision')
        implementation project(path: ':VisionAr')
        implementation project(path: ':VisionSafety')
        implementation project(path: ':CoreAr')
        implementation project(path: ':CoreSdk')
        implementation project(path: ':CoreSafety')
    } else {
        implementation "com.mapbox.vision:mapbox-android-vision:$vision_sdk"
        implementation "com.mapbox.vision:mapbox-android-vision-ar:$vision_ar"
        implementation "com.mapbox.vision:mapbox-android-vision-safety:$vision_safety"
    }

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
}
