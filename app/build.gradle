apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: 'generate-token.gradle'

def envVarOrLocalProperty(String name) {
    if (System.getenv(name) != null) {
        return System.getenv(name).toBoolean()
    }

    Properties properties = new Properties()
    def file = project.rootProject.file('local.properties')
    if (file.exists()) {
        properties.load(file.newDataInputStream())
        def isLocalDevelopment = properties.getProperty(name)
        if (isLocalDevelopment != null) {
            return isLocalDevelopment.toBoolean()
        }
    }
    return false
}

if (file("${rootDir}/gradle/versions.gradle").exists()) {
    apply from: "${rootDir}/gradle/versions.gradle"
}

android {
    buildToolsVersion build_tools_version
    compileSdkVersion compile_sdk_version

    defaultConfig {
        applicationId "com.mapbox.vision.examples"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$support_lib"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"

    implementation 'com.android.support:multidex:1.0.3'

    implementation("com.mapbox.mapboxsdk:mapbox-android-sdk:$mapbox_sdk") {
        exclude module: 'mapbox-android-telemetry'
    }
    implementation("com.mapbox.mapboxsdk:mapbox-android-navigation:$mapbox_navigation") {
        exclude module: 'mapbox-android-telemetry'
    }
    implementation("com.mapbox.mapboxsdk:mapbox-android-navigation-ui:$mapbox_navigation") {
        exclude module: 'mapbox-android-telemetry'
    }
    implementation("com.mapbox.mapboxsdk:mapbox-android-plugin-locationlayer:$mapbox_location_layer") {
        exclude module: 'mapbox-android-telemetry'
    }
    implementation("com.mapbox.mapboxsdk:mapbox-android-core:$mapbox_core")

    implementation 'com.mapbox.vision:mapbox-android-vision:0.1.0'
    implementation 'com.mapbox.vision:mapbox-android-vision-ar:0.1.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
